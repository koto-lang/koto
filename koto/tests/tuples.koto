from test import assert, assert_eq

export tests =
  test_for_loop: ||
    a = 1, 2, 3
    z = []
    for x in a
      z.push x
    assert_eq z [1, 2, 3]

  test_contains: ||
    x = 1, 2, 3
    assert (x.contains 2)
    assert not (x.contains 99)

  test_deep_copy: ||
    a = [1, 2, 3]
    # x contains 3 shared copies of a
    x = (a, a, a)
    # deep_copy creates 3 unique copies of a
    x2 = x.deep_copy()
    # modifying a is reflected in x, but not x2
    a[0] = 99
    assert_eq x[1][0] 99
    assert_eq x2[0][0] 1
    # modifying one list in x2 doesn't affect the others
    x2[1][0] = 42
    assert_eq x2[0][0] 1

  test_get: ||
    x = 1, 2, 3
    assert_eq (x.get 0) 1
    assert_eq (x.get 2) 3
    assert_eq (x.get 4) ()

  test_indexing: ||
    x = 1, 2, 3
    assert_eq x[0] 1
    assert_eq x[2] 3
    assert_eq x[..] x
    assert_eq x[0..2] (1, 2)
    assert_eq x[1..] (2, 3)
    assert_eq x[..=1] (1, 2)

  test_iter: ||
    assert_eq
      (1, 2, 3)
        .each |n| "{}".format n
        .to_tuple()
      ("1", "2", "3")

  test_size: ||
    assert_eq (1, 2).size() 2
    assert_eq (1, 2, 3).size() 3
    assert_eq ((1, 2), (3, 4)).size() 2

  test_sort_copy: ||
    assert_eq (3, 1, 2).sort_copy() (1, 2, 3)
    assert_eq ("tuple", "sort", "copy").sort_copy() ("copy", "sort", "tuple")

  test_to_list: ||
    assert_eq (1, 2).to_list() [1, 2]
    assert_eq ((1, 2), (3, 4)).to_list() [(1, 2), (3, 4)]
